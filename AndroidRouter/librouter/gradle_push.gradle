apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = "com.joybar.router"
version = "1.0.7"

def siteUrl = 'https://github.com/myjoybar/Android-Router'
def issuesUrl = 'https://github.com/myjoybar/Android-Router/issues'
def gitUrl = 'https://github.com/myjoybar/Android-Router.git'
def ARTIFACT_ID = 'librouter'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                description 'A simple and easy to use Android Router'
                name ARTIFACT_ID
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'joybar'
                        name 'joybar'
                        email 'myjoybar@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()

properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "Andorid-Maven"
        name = ARTIFACT_ID
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issuesUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
//./gradlew install
//./gradlew bintrayUpload

//./gradlew javadocJar
//./gradlew sourcesJar
//./gradlew install
//./gradlew bintrayUpload
